#================================================================
# cmake utilities to build python-bindings for mechanics component
#================================================================

# wrap - Set extra dependencies
set(SWIG_MODULE_${COMPONENT}_EXTRA_DEPS)

# DEFS
set(${COMPONENT}_SWIG_DEFS "${SWIG_DEFS}")

# List of modules to be built in the python bindings
set(${COMPONENT}_PYTHON_MODULES
  "mechanics/joints"
  "mechanics/collision/base"
  "mechanics/collision/native"
  "mechanics/collision/bodies")

if(WITH_OCC)
  list(APPEND ${COMPONENT}_PYTHON_MODULES "mechanics/occ")
endif()

if(WITH_BULLET)
  list(APPEND ${COMPONENT}_PYTHON_MODULES "mechanics/collision/bullet")
  list(APPEND ${COMPONENT}_SWIG_DEFS "-DWITH_BULLET")

  # ignore all warnings generated by Bullet headers since we can't
  # do anything about them and it clutters the output
  list(APPEND ${COMPONENT}_SWIG_DEFS_bullet
    "-w312,322,325,350,351,362,383,389,394,395,401,402,403,503,512,520")

  if(BULLET_USE_DOUBLE_PRECISION)
    list(APPEND ${COMPONENT}_SWIG_DEFS "-DBT_USE_DOUBLE_PRECISION")
  endif()
endif()

if(WITH_MECHANISMS)
  list(APPEND ${COMPONENT}_PYTHON_MODULES
    "mechanics/mechanisms/cadmbtb"
    "mechanics/mechanisms/mbtb")
endif()

set(${COMPONENT}_SWIG_INCLUDE_DIRECTORIES
  ${CMAKE_SOURCE_DIR}/kernel/swig)

configure_file(mechanics/collision/tools.py ${SICONOS_SWIG_ROOT_DIR}/mechanics/collision/tools.py)
configure_file(mechanics/collision/convexhull.py  ${SICONOS_SWIG_ROOT_DIR}/mechanics/collision/convexhull.py )
configure_file(mechanics/collision/__init__.py  ${SICONOS_SWIG_ROOT_DIR}/mechanics/collision/__init__.py )
include(swig_python_tools)
swig_module_setup(${COMPONENT}_PYTHON_MODULES)
# exclude mechanisms tests. They will be activated later (below) if required.
if(NOT WITH_MECHANISMS)
  list(APPEND ${COMPONENT}_python_excluded_tests
    ${CMAKE_CURRENT_SOURCE_DIR}/tests/test_siconos_mechanisms.py)
endif()
build_python_tests()


# siconos mechanisms script
if(WITH_MECHANISMS)
  # we need siconos-mechanism for build and install tree with different 'PYTHONPATH' for each of them.

  # -- prepare siconos_mechanisms tests --
  set(CURRENT_PYTHON_PATH ${CMAKE_BINARY_DIR}/wrap)
  set(EXAMPLE_PATH ${SICONOS_SWIG_ROOT_DIR}/tests/)
  # Each file is copy to siconos/tests.
  # Maybe we can create a 'tests' dir for each subpackage?
  # --> Easier to deal with plugins and data if only one package
  configure_file(mechanics/mechanisms/siconos_mechanisms.py.in  ${SICONOS_SWIG_ROOT_DIR}/tests/siconos_mechanisms.py)
  set(name "python_test_siconos_mechanisms")
  set(exename ${SICONOS_SWIG_ROOT_DIR}/tests/test_siconos_mechanisms.py)
  add_python_test(${name}, ${exename})
  # data (CAD and body) taken from slider crank example
  set(path_to_test_sources ${CMAKE_SOURCE_DIR}/examples/Mechanics/Mechanisms/SliderCrank)
  configure_file(${path_to_test_sources}/bodydef.py ${SICONOS_SWIG_ROOT_DIR}/tests/ COPYONLY)
  configure_file(${CMAKE_CURRENT_SOURCE_DIR}/tests/mbtbLocalOptions.py ${SICONOS_SWIG_ROOT_DIR}/tests/ COPYONLY)
  file(GLOB cad4tests RELATIVE ${path_to_test_sources}/CAD ${path_to_test_sources}/CAD/*)
  foreach(datafile ${cad4tests})
    configure_file(${path_to_test_sources}/CAD/${datafile}
      ${SICONOS_SWIG_ROOT_DIR}/tests/CAD/${datafile} COPYONLY)
  endforeach()

  # -- install the script --
  set(CURRENT_PYTHON_PATH ${SICONOS_PYTHON_INSTALL_DIR})
  set(EXAMPLE_PATH ./)
  configure_file(mechanics/mechanisms/siconos_mechanisms.py.in ${CMAKE_BINARY_DIR}/scripts/siconos_mechanisms.py)
  configure_file(mechanics/mechanisms/mbtbDefaultOptions.py ${SICONOS_SWIG_ROOT_DIR}/mechanics/mechanisms/mbtbDefaultOptions.py)
  install(PROGRAMS ${CMAKE_BINARY_DIR}/scripts/siconos_mechanisms.py DESTINATION bin RENAME siconos_mechanisms)
endif()
