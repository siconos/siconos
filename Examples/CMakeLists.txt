# cmake -DCMAKE_INSTALL_PREFIX=<your installation dir> <path>
CMAKE_MINIMUM_REQUIRED(VERSION 2.4)
SET(CMAKE_MODULE_PATH ${CMAKE_SOURCE_DIR}/cmake)

# CMP0011 policy before first include
# others policies are set in SiconosProject
IF(POLICY CMP0011)
  CMAKE_POLICY(SET CMP0011 NEW)
ENDIF(POLICY CMP0011)
INCLUDE(SiconosProject)

ENABLE_LANGUAGE(C)
ENABLE_LANGUAGE(CXX)
ENABLE_LANGUAGE(Fortran) 

SET(EXAMPLES_DIRECTORIES
  Control
  Electronics
  Mechanics
  Numerics
  Robotics/HuMAns_pa10
)

SET(NO_TEST_FILES QGL Two)

SICONOS_PROJECT(Examples 3 2 0)

COMPILE_WITH(SiconosNumerics REQUIRED)
COMPILE_WITH(SiconosKernel REQUIRED)

#
# Internals
#


MACRO(ADD_EXAMPLE_DIRECTORY _N)
  MESSAGE("Adding example ${_N}")
  FILE(MAKE_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/${_N})
  CONFIGURE_FILE(${CMAKE_SOURCE_DIR}/cmake/CMakeListsForExamples.cmake 
    ${CMAKE_CURRENT_BINARY_DIR}/${_N}/CMakeLists.txt @ONLY)
  ADD_SUBDIRECTORY(${CMAKE_CURRENT_BINARY_DIR}/${_N} 
    ${CMAKE_CURRENT_BINARY_DIR}/${_N})
ENDMACRO(ADD_EXAMPLE_DIRECTORY _N)

FOREACH(_D ${EXAMPLES_DIRECTORIES})
  FILE(GLOB EXAMPLES_P ${CMAKE_SOURCE_DIR}/${_D}/*/*.cpp )
  IF(EXAMPLES_P)
    FOREACH(_P ${EXAMPLES_P})
      GET_FILENAME_COMPONENT(_RP ${_P} ABSOLUTE)
      LIST(APPEND EXAMPLES ${_RP})
    ENDFOREACH(_P ${EXAMPLES_P})
  ENDIF(EXAMPLES_P)
ENDFOREACH(_D ${EXAMPLES_DIRECTORIES})

FOREACH(EXAMPLE ${EXAMPLES})
  IF(NOT EXAMPLE MATCHES Plugin)
    SET(TEST_ME TRUE)
    FOREACH(_NT ${NO_TEST_FILES})
      IF(EXAMPLE MATCHES "${_NT}")
        SET(TEST_ME FALSE)
      ENDIF(EXAMPLE MATCHES "${_NT}")
    ENDFOREACH(_NT ${NO_TEST_FILES})
    IF(TEST_ME)
      GET_FILENAME_COMPONENT(EXAMPLE_NAME ${EXAMPLE} NAME_WE)
      ADD_EXAMPLE_DIRECTORY(${EXAMPLE_NAME})
    ENDIF(TEST_ME)
  ENDIF(NOT EXAMPLE MATCHES Plugin)
ENDFOREACH(EXAMPLE ${EXAMPLES})
